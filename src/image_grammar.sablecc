Package org.dynamos.image;

Helpers
  digit = ['0'..'9'];
  alpha_numeric = [[[['a'..'z'] + ['A'..'Z']] + digit] + '_'];

Tokens
  colon = ':';
  mnemonic = ('CALL' | 'OBJ' | 'PUSH' | 'SYM' | 'VAL' | 'START' | 'END' );
  number = digit+;
  id = alpha_numeric (alpha_numeric | ':')*;
  open_brace = '{';
  close_brace = '}';
  fullstop = '.';
  dollar = '$';
  hash = '#';
  equals = '=';
  blank = (' ' | 13 | 10)+;
  
Ignored Tokens
  blank;
  
Productions
  image = object_definition*;
  object_definition = {slot}      id slots functions |
                      {reference} id dollar references;
  references = {continue} id fullstop references |
               {end} id dollar;
  slots = open_brace image close_brace;
  functions = open_brace function_definition* close_brace;
  function_definition = id required_parameters opcodes;
  required_parameters = open_brace required_parameter* close_brace;
  required_parameter = id fullstop;
  opcodes = open_brace opcode* close_brace;
  opcode = opcode_id fullstop;
  opcode_id = {number} number opcode_parameter |
              {opcode} mnemonic opcode_parameter;
  opcode_parameter = 
    {symbol} equals id |
    {value} hash number |
    {empty};
  
